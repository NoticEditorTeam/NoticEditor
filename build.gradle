plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

sourceCompatibility = 11
targetCompatibility = 11
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
ext.mainPackage = 'com.noticeditorteam.noticeditor'
mainClassName = "${ext.mainPackage}.Main"
version '1.3-SNAPSHOT'

application {
    applicationDefaultJvmArgs = [
        '--add-opens=javafx.controls/javafx.scene.control=ALL-UNNAMED'
    ]
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['src']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
    }
}

def platform = System.getProperty('os.name').toLowerCase();
if (platform.contains('windows')) {
    platform = 'windows'
} else if (platform.contains('linux')) {
    platform = 'linux'
} else {
    platform = 'mac'
}

jar {
    archiveClassifier = platform
    manifest {
        attributes 'Main-Class': "${mainPackage}.Launcher"
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = duplicatesStrategy.INCLUDE
}

javafx {
    version = '14.0.1'
    modules = [
        'javafx.controls', 'javafx.fxml',
        'javafx.swing', 'javafx.web'
    ]
}

ext.flexmarkVersion = '0.62.2'

dependencies{
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation 'net.lingala.zip4j:zip4j:2.6.1'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'org.json:json:20200518'
    implementation "com.vladsch.flexmark:flexmark:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-ext-autolink:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-ext-tables:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-ext-gfm-tasklist:$flexmarkVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'com.carrotsearch:junit-benchmarks:0.7.2'
}
